#############
# DESCRIPTION
#############

# [DS]: default state
# [FS]: final state
# [OTT]: once timed transition
# [RTT]: repeated timed transition

# S: source
# N: destination
# E: event
# O: commands
# C: condition
# T: timed transition

#################
# DESCRIPTION END
#################

INITIALIZER:
    from Tkinter import *
    from DWatchGUI import DWatchGUI
    debugger.InterruptEvent('GUI Debug')

    root = Tk()
    root.title("A lovely watch")
    g = DWatchGUI(root, eventhandler)

    eventhandler.start()

    root.mainloop()
    exit()

STATECHART:
    Initial[DS]
    Started
      Time[CS][DS]
        TimeDisplay[DS]
        TimeEdit
      Alarm
        AlarmDisplay
        AlarmEdit
      Chrono
        ChronoStopped
        ChronoRunning
      Light[CS][DS]
        Off[DS]
        On

TRANSITION:
    S: Initial
    N: Started.Time.TimeDisplay
    T: 1 [OTT]
    O: [DUMP("start The Time")]

#######
# LIGHT
#######

TRANSITION:
    S: Started.Light.Off
    N: Started.Light.On
    E: lightOn
    O: g.setIndiglo() 

TRANSITION:
    S: Started.Light.On
    N: Started.Light.Off
    E: lightOff
    O: g.unsetIndiglo()

###########
# LIGHT END
###########

##################
# TIME TRANSITIONS
##################

TRANSITION:
    S: Started.Time.TimeDisplay
    N: Started.Time.TimeDisplay
    T: 1  # by default, it is [RTT]
    O: g.increaseTimeByOne()

TRANSITION:
    S: Started.Time.TimeDisplay
    N: Started.Time.TimeEdit
    E: editTime
    O: g.startSelection()
       g.setToEditTimeInProgress(True)

TRANSITION:
    S: Started.Time.TimeEdit
    N: Started.Time.TimeEdit
    E: released
    O: g.setToEditTimeInProgress(False)
       g.startAutoFinishEditTimeTimer()
       g.endFinishEditTimeTimer()

TRANSITION:
    S: Started.Time.TimeEdit
    N: Started.Time.TimeEdit
    E: bottomLeftReleased
    O: g.startAutoFinishEditTimeTimer()

TRANSITION:
    S: Started.Time.TimeEdit
    N: Started.Time.TimeEdit
    E: topLeftReleased
    O: g.startAutoFinishEditTimeTimer()

TRANSITION:
    S: Started.Time.TimeEdit
    N: Started.Time.TimeEdit
    E: selectNext
    O: g.selectNext()
       g.endAutoFinishEditTimeTimer()

TRANSITION:
    S: Started.Time.TimeEdit
    N: Started.Time.TimeEdit
    E: increase
    O: g.increaseSelection()
       g.endAutoFinishEditTimeTimer()

TRANSITION:
    S: Started.Time.TimeEdit
    N: Started.Time.TimeEdit
    C: g.getIncreasePressed()
    T: 0.3 [RTT]
    O: g.increaseSelection()

TRANSITION:
    S: Started.Time.TimeEdit
    N: Started.Time.TimeEdit
    E: bottomRightPressed
    O: g.endAutoFinishEditTimeTimer()

TRANSITION:
    S: Started.Time.TimeEdit
    N: Started.Time.TimeDisplay
    E: finishEdit
    C: not g.getToEditTimeInProgress()
    O: g.stopSelection()
       g.endAutoFinishEditTimeTimer()

######################
# TIME TRANSITIONS END
######################


######################
# CHRONO TRANSITIONS
######################

### change mode from time to stopped chrono
TRANSITION:
    S: Started.Time.TimeDisplay
    N: Started.Chrono.ChronoStopped
    E: changeMode
    C: g.getChrono() is False
    O: g.refreshChronoDisplay()
    [DUMP("change mode from time to chrono")]

### change mode from time to running chrono
TRANSITION:
    S: Started.Time.TimeDisplay
    N: Started.Chrono.ChronoRunning
    E: changeMode
    C: g.getChrono() is True
    O: g.refreshChronoDisplay()
    [DUMP("change mode from time to chrono")]

### keep time running in the background when using chrono
TRANSITION:
    S: Started.Chrono.ChronoStopped
    N: Started.Chrono.ChronoStopped
    T: 1  # by default, it is [RTT]
    O: g.increaseTimeByOne()
       g.refreshChronoDisplay()

### start chrono with bottom right button
TRANSITION:
    S: Started.Chrono.ChronoStopped
    N: Started.Chrono.ChronoRunning
    E: initChrono
    O: g.setChrono(True)

### chrono is running
TRANSITION:
    S: Started.Chrono.ChronoRunning
    N: Started.Chrono.ChronoRunning
    T: 1  # by default, it is [RTT]
    O: g.increaseTimeByOne()
       g.increaseChronoByOne()
       g.refreshChronoDisplay()

### stop chrono with bottom right button
TRANSITION:
    S: Started.Chrono.ChronoRunning
    N: Started.Chrono.ChronoStopped
    E: initChrono
    O: g.setChrono(False)

### reset chrono with bottom left button
TRANSITION:
    S: Started.Chrono.ChronoStopped
    N: Started.Chrono.ChronoStopped
    E: resetChrono
    O: g.resetChrono()

### reset chrono with bottom left button
TRANSITION:
    S: Started.Chrono.ChronoRunning
    N: Started.Chrono.ChronoRunning
    E: resetChrono
    O: g.resetChrono()

### change mode from stopped chrono to time
TRANSITION:
    S: Started.Chrono.ChronoStopped
    N: Started.Time.TimeDisplay
    E: changeMode
    O: [DUMP("change mode from chrono to time")]

### change mode from running chrono to time
TRANSITION:
    S: Started.Chrono.ChronoRunning
    N: Started.Time.TimeDisplay
    E: changeMode
    O: [DUMP("change mode from chrono to time")]

### chrono running in the background
TRANSITION:
    S: Started.Time.TimeDisplay
    N: Started.Time.TimeDisplay
    C: g.getChrono() is True
    T: 1  # by default, it is [RTT]
    O: g.increaseChronoByOne()


######################
# CHRONO TRANSITIONS END
######################